/*
 * BX-bot JBehave Gradle build.
 */

plugins {
    id 'java'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'com.fasterxml.jackson.core:jackson-core:2.10.3'
    testImplementation 'com.google.guava:guava:27.1-jre'
    testImplementation 'javax.validation:validation-api:2.0.0.Final'
    testImplementation 'org.apache.logging.log4j:log4j-api:2.13.1'
    testImplementation 'org.apache.logging.log4j:log4j-core:2.13.1'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.jbehave:jbehave-core:4.6'
    testImplementation 'org.jbehave.site:jbehave-site-resources:3.1.1:@zip'
    testImplementation 'org.apache.httpcomponents:httpclient:4.5.11'
    testImplementation 'org.apache.httpcomponents:httpcore:4.4.13'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.10.3'
}

group = 'com.gazbert'
version = '0.0.1-SNAPSHOT'
description = 'BX-bot REST API JBehave Tests'
sourceCompatibility = '11'

test {
    systemProperty "metaFilters", System.getProperty("metaFilters", "")

    doFirst {
        copy {
            from(zipTree(jarPath("jbehave-core"))) {
                include "style/*"
            }
            into("build/jbehave/view")

        }
        copy {
            from(zipTree(jarPath("jbehave-site-resources"))) {
                include "js/**/*"
                include "style/**/*"
                include "images/*"
            }
            into("build/jbehave/view")
        }
    }
}

def jarPath(String jarName) {
    configurations.testCompileClasspath.find({ it.name.startsWith(jarName) }).absolutePath
}
